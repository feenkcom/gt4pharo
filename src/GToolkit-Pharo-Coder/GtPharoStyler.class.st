Class {
	#name : #GtPharoStyler,
	#superclass : #GtGenericPharoStyler,
	#category : #'GToolkit-Pharo-Coder-Styler'
}

{ #category : #accessing }
GtPharoStyler >> parenthesesColors [
	| colors |
	colors := BrGlamorousColors distinctTenStrongColors allButFirst
		collect: [ :each | each twiceDarker ].
	^ colors
]

{ #category : #accessing }
GtPharoStyler >> visitArray: anArray [
	| colors color |
	super visitArray: anArray.
	colors := self parenthesesColors.
	color := colors at: (anArray nestingLevel - 1) \\ colors size + 1.
	(text
		from: anArray leftBrace startPosition
		to: anArray leftBrace stopPosition) foreground: color.
	(text
		from: anArray rightBrace startPosition
		to: anArray rightBrace stopPosition) foreground: color
]

{ #category : #accessing }
GtPharoStyler >> visitArrayLiteral: anArrayLiteral [
	| colors color |
	super visitArrayLiteral: anArrayLiteral.
	colors := self parenthesesColors.
	color := colors at: (anArrayLiteral nestingLevel - 1) \\ colors size + 1.
	(text
		from: anArrayLiteral arrayStart startPosition
		to: anArrayLiteral arrayStart stopPosition) foreground: color.
	(text
		from: anArrayLiteral arrayStop startPosition
		to: anArrayLiteral arrayStop stopPosition) foreground: color
]

{ #category : #accessing }
GtPharoStyler >> visitBlock: aBlock [
	| color colors |
	super visitBlock: aBlock.
	colors := self parenthesesColors.
	color := colors at: (aBlock nestingLevel - 1) \\ colors size + 1.
	(text from: aBlock leftBracket startPosition to: aBlock leftBracket startPosition)
		foreground: color.
	(text from: aBlock rightBracket startPosition to: aBlock rightBracket startPosition)
		foreground: color.
]

{ #category : #visiting }
GtPharoStyler >> visitBooleanLiteral: aBooleanLiteral [
	super visitBooleanLiteral: aBooleanLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color red muchDarker }
		from: aBooleanLiteral value startPosition
		to: aBooleanLiteral value stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitCharacterLiteral: aCharacterLiteral [
	super visitCharacterLiteral: aCharacterLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color red muchDarker }
		from: aCharacterLiteral value startPosition
		to: aCharacterLiteral value stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitCommentFrom: aCommentStart to: aCommentEnd [
	self
		attributes: { BlTextForegroundAttribute color: Color gray darker }
		from: aCommentStart
		to: aCommentEnd
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitDeclaredGlobalVariable: aNamedVariable [
	self visitDeclaredVariable: aNamedVariable
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitDeclaredVariable: aNamedVariable [
	self
		attributes: { BlTextForegroundAttribute color: Color blue muchDarker }
		from: aNamedVariable name startPosition
		to: aNamedVariable name stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitError: anError [
	anError smaccError acceptVisitor: self
]

{ #category : #visiting }
GtPharoStyler >> visitErrorToken: anErrorToken [
	self
		attributes: { BlTextForegroundAttribute color: Color red }
		from: anErrorToken startPosition
		to: anErrorToken stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitMethodPattern: aMethodPattern [
	super visitMethodPattern: aMethodPattern.
	aMethodPattern selectorParts do: [ :eachSelectorPart |
		self visitMethodSelectorPart: eachSelectorPart ].

	super visitMethodPattern: aMethodPattern
]

{ #category : #visiting }
GtPharoStyler >> visitMethodSelectorPart: aSmaCCToken [
	self
		attributes: { BlFontWeightAttribute bold }
		from: aSmaCCToken startPosition
		to: aSmaCCToken stopPosition
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitNamedVariable: aNamedVariable [
	| isDeclaredVariable |
	super visitNamedVariable: aNamedVariable.
	isDeclaredVariable := aNamedVariable isUndeclared not.
	isDeclaredVariable
		ifTrue: [ ^ self visitDeclaredVariable: aNamedVariable ].
	
	isDeclaredVariable := self coderViewModel hasDeclaredVariableNamed: aNamedVariable variableName.
	isDeclaredVariable
		ifTrue: [ ^ self visitDeclaredVariable: aNamedVariable ].
	
	isDeclaredVariable := self coderViewModel hasDeclaredGlobalNamed: aNamedVariable variableName.
	isDeclaredVariable
		ifTrue: [ ^ self visitDeclaredGlobalVariable: aNamedVariable ].

	self visitUndeclaredVariable: aNamedVariable
]

{ #category : #visiting }
GtPharoStyler >> visitNilLiteral: aNilLiteral [
	super visitNilLiteral: aNilLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color red muchDarker }
		from: aNilLiteral value startPosition
		to: aNilLiteral value stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitNumberLiteral: aNumberLiteral [
	super visitNumberLiteral: aNumberLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color red muchDarker }
		from: aNumberLiteral value startPosition
		to: aNumberLiteral value stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitPragma: aPragma [
	super visitPragma: aPragma.
	self visitPragmaSelectorPart: aPragma lessThan.
	aPragma selectorParts do: [ :eachSelectorPart |
		self visitPragmaSelectorPart: eachSelectorPart ].
	self visitPragmaSelectorPart: aPragma greaterThan.
	
	aPragma values do: [ :eachPragmaValue |
		self acceptNode: eachPragmaValue ]
]

{ #category : #visiting }
GtPharoStyler >> visitPragmaSelectorPart: aSmaCCToken [
	self
		attributes: { BlTextForegroundAttribute color: Color green muchDarker }
		from: aSmaCCToken startPosition
		to: aSmaCCToken stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitReturn: aReturn [
	super visitReturn: aReturn.

	self visitReturnUpArrow: aReturn upArrow
]

{ #category : #visiting }
GtPharoStyler >> visitReturnUpArrow: aSmaCCToken [
	self
		attributes: { BlTextForegroundAttribute color: Color cyan muchDarker }
		from: aSmaCCToken startPosition
		to: aSmaCCToken stopPosition
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitSelfVariable: aSelfVariable [
	super visitSelfVariable: aSelfVariable.
	self
		attributes: { BlTextForegroundAttribute color: Color cyan muchDarker }
		from: aSelfVariable name startPosition
		to: aSelfVariable name stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitSmaCCError: anErrorNode [
	anErrorNode
		nodesDo: [ :eachNode | self acceptNode: eachNode ]
		andTokensDo: [ :eachToken | ]
		includeErrors: true.
	anErrorNode dismissedTokens do: [ :eachToken | self visitErrorToken: eachToken ]
]

{ #category : #visiting }
GtPharoStyler >> visitSmaCCParseNode: aSmaCCParseNode [
	super visitSmaCCParseNode: aSmaCCParseNode.

	aSmaCCParseNode comments ifNotNil: [ :theComments |
		theComments do: [ :eachComment |
			self visitCommentFrom: eachComment first to: eachComment second ] ]
]

{ #category : #visiting }
GtPharoStyler >> visitStringLiteral: aStringLiteral [
	super visitStringLiteral: aStringLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color magenta muchDarker }
		from: aStringLiteral value startPosition
		to: aStringLiteral value stopPosition
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitSuperVariable: aSuperVariable [
	super visitSuperVariable: aSuperVariable.
	self
		attributes: { BlTextForegroundAttribute color: Color cyan muchDarker }
		from: aSuperVariable name startPosition
		to: aSuperVariable name stopPosition
]

{ #category : #visiting }
GtPharoStyler >> visitSymbolLiteral: aSymbolLiteral [
	super visitSymbolLiteral: aSymbolLiteral.
	self
		attributes: { BlTextForegroundAttribute color: Color magenta muchDarker }
		from: aSymbolLiteral value startPosition
		to: aSymbolLiteral value stopPosition
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitThisContextVariable: aThisContextVariable [
	super visitThisContextVariable: aThisContextVariable.
	self
		attributes: { BlTextForegroundAttribute color: Color cyan muchDarker }
		from: aThisContextVariable name startPosition
		to: aThisContextVariable name stopPosition
]

{ #category : #'visiting - variables' }
GtPharoStyler >> visitUndeclaredVariable: aVariableNode [
	self
		attributes: { BlTextForegroundAttribute color: Color red }
		from: aVariableNode name startPosition
		to: aVariableNode name stopPosition
]

{ #category : #accessing }
GtPharoStyler >> visitValue: aValue [
	| colors color |
	super visitValue: aValue.
	colors := self parenthesesColors.
	aValue leftParens doWithIndex: [ :each :i |
		color := colors at: (aValue nestingLevel + i - 1 - aValue leftParens size "because its part of nesting") \\ colors size + 1.
		(text from: each startPosition to: each startPosition) foreground: color.
		(text 
			from: (aValue rightParens at: (aValue rightParens size - i + 1)) startPosition 
			to: (aValue rightParens at: (aValue rightParens size - i + 1)) startPosition)
			foreground: color.
	]
]
